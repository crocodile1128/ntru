{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "-2*x^736 - 2*x^728 - 2*x^706 + 2*x^705 + 2*x^696 - 2*x^693 + 4*x^688 - 2*x^687 - 2*x^678 + 2*x^676 + 2*x^675 + 2*x^674 + 2*x^672 - 2*x^670 - 2*x^667 - 2*x^664 - 2*x^658 - 2*x^654 - 2*x^651 + 2*x^649 - 2*x^645 + 2*x^633 - 2*x^629 + 2*x^628 + 2*x^627 + 2*x^619 - 2*x^617 - 2*x^616 + 2*x^611 + 2*x^608 + 2*x^605 - 2*x^604 + 2*x^603 - 2*x^602 + 2*x^599 + 2*x^595 + 2*x^592 - 4*x^590 + 2*x^588 - 2*x^587 - 4*x^585 + 2*x^584 - 2*x^581 - 2*x^577 - 2*x^574 - 2*x^569 - 2*x^568 + 2*x^567 + 2*x^566 + 2*x^563 + 4*x^562 - 2*x^561 + 2*x^559 - 2*x^557 - 2*x^552 + 4*x^551 + 2*x^549 + 2*x^547 + 2*x^543 - 2*x^542 - 2*x^541 - 2*x^540 + 2*x^539 - 2*x^537 + 2*x^536 - 2*x^531 - 2*x^530 + 2*x^529 - 2*x^528 + 2*x^527 - 4*x^525 + 2*x^523 - 2*x^522 - 2*x^520 + 4*x^519 + 2*x^518 + 2*x^516 - 4*x^514 - 2*x^512 - 6*x^508 + 2*x^507 - 2*x^506 + 2*x^503 - 4*x^502 + 4*x^501 - 2*x^499 - 2*x^498 - 2*x^494 + 2*x^493 - 2*x^492 - 2*x^490 + 2*x^487 + 2*x^484 + 2*x^483 - 2*x^482 + 2*x^481 - 2*x^480 + 2*x^479 + 2*x^478 - 2*x^476 - 2*x^474 - 2*x^473 + 2*x^472 + 4*x^471 + 2*x^470 + 4*x^466 - 4*x^465 - 2*x^464 + 2*x^463 + 2*x^462 - 4*x^460 - 2*x^459 + 4*x^458 + 2*x^453 - 2*x^451 + 2*x^450 + 2*x^449 - 2*x^448 + 2*x^447 - 2*x^444 + 2*x^443 - 2*x^442 + 4*x^441 + 2*x^439 + 4*x^438 + 2*x^436 - 2*x^434 - 2*x^433 - 4*x^431 - 2*x^429 + 2*x^428 + 2*x^426 - 2*x^425 - 2*x^423 - 2*x^419 - 2*x^417 - 2*x^413 - 2*x^412 - 2*x^411 + 8*x^406 - 2*x^405 - 2*x^401 + 2*x^398 + 2*x^387 + 2*x^386 - 2*x^384 + 2*x^383 - 4*x^382 - 2*x^380 - 2*x^379 + 2*x^378 + 2*x^376 + 2*x^375 + 2*x^374 - 4*x^373 + 2*x^372 + 4*x^370 - 6*x^369 + 2*x^368 - 2*x^365 + 2*x^364 + 2*x^362 - 2*x^360 + 2*x^355 - 2*x^353 - 2*x^350 - 2*x^349 + 2*x^348 - 2*x^347 + 2*x^344 - 2*x^343 + 2*x^342 + 2*x^341 - 2*x^340 - 4*x^331 + 2*x^330 + 2*x^328 - 2*x^323 + 2*x^322 - 2*x^321 + 4*x^320 - 2*x^317 - 2*x^315 + 2*x^312 + 4*x^310 - 2*x^309 - 2*x^308 - 2*x^307 + 4*x^303 + 2*x^302 + 2*x^301 - 2*x^300 + 2*x^298 + 2*x^297 + 2*x^295 + 4*x^294 - 2*x^293 - 2*x^292 + 4*x^291 - 2*x^289 + 2*x^287 + 2*x^286 - 2*x^285 + 2*x^284 + 2*x^283 - 2*x^282 + 6*x^279 - 2*x^278 - 2*x^276 - 2*x^274 - 2*x^273 + 2*x^271 - 2*x^270 - 2*x^267 - 2*x^266 + 2*x^262 - 2*x^261 - 2*x^260 + 2*x^257 - 2*x^256 - 2*x^252 + 2*x^251 - 2*x^243 - 2*x^242 - 2*x^239 - 2*x^238 + 2*x^237 + 2*x^233 - 2*x^232 - 2*x^230 + 2*x^228 - 2*x^226 + 2*x^225 - 4*x^224 + 2*x^223 + 2*x^220 + 4*x^219 + 2*x^217 + 6*x^215 - 2*x^214 - 2*x^209 - 2*x^208 - 4*x^207 - 2*x^206 - 4*x^205 + 2*x^204 + 2*x^203 + 2*x^202 - 2*x^201 + 2*x^200 - 2*x^199 - 2*x^197 - 2*x^195 - 2*x^192 + 2*x^191 + 2*x^190 - 2*x^188 - 4*x^187 - 2*x^186 + 2*x^185 - 2*x^183 - 2*x^181 - 4*x^180 + 2*x^178 + 4*x^177 + 2*x^176 - 2*x^175 - 2*x^172 - 2*x^167 + 2*x^165 - 4*x^164 + 2*x^162 + 2*x^161 + 2*x^160 + 2*x^158 - 4*x^157 + 2*x^156 + 2*x^155 + 2*x^154 + 2*x^150 + 2*x^146 + 2*x^143 + 2*x^139 + 2*x^137 + 2*x^131 + 2*x^128 + 2*x^127 + 2*x^126 - 2*x^119 - 2*x^118 - 2*x^117 - 2*x^114 + 2*x^111 + 2*x^109 - 2*x^106 - 2*x^105 - 2*x^104 - 2*x^102 - 2*x^100 + 2*x^96 + 6*x^94 + 2*x^91 + 2*x^89 - 2*x^87 - 2*x^86 - 2*x^85 - 2*x^84 + 2*x^77 - 2*x^75 + 2*x^72 - 2*x^67 + 2*x^61 + 2*x^60 + 2*x^59 - 2*x^54 + 2*x^51 + 2*x^49 + 2*x^42 - 4*x^35 - 2*x^33 + 2*x^18 - 2*x^16 + 1\n",
      "0.573862075806 0.00103998184204 0.00134491920471\n",
      "True\n",
      "True\n"
     ]
    }
   ],
   "source": [
    "from time import time\n",
    "R.<x> = ZZ[];\n",
    "\n",
    "class NTRUEncrypt(object):\n",
    "\n",
    "    def sample(self,NN, o,mo):\n",
    "        s=[1]*o+[-1]*mo+[0]*(NN-o-mo)\n",
    "        shuffle(s)\n",
    "        return R(s)\n",
    "\n",
    "    def __pn__(self,d1,d2,d3):\n",
    "        a=ZZ.random_element(2*d1, self.n-d2)\n",
    "        p1=self.sample(a, d1, d1)\n",
    "        p2=self.sample(self.n-a, d2, d2)\n",
    "        p3=self.sample(self.n, d3, d3)\n",
    "        poly=p1*p2+p3\n",
    "        return poly\n",
    "\n",
    "    #reduces the coefficients of polynomial f modulo pp\n",
    "    #and \"fixes\" them so that they belong to [-pp/2,pp/2]\n",
    "    def modCoeffs(self,f,pp):\n",
    "        clist=f.list()\n",
    "        p2=pp/2\n",
    "        for i in range(len(clist)):\n",
    "            clist[i] = clist[i]%pp\n",
    "            if clist[i]>p2:\n",
    "                clist[i]-=pp\n",
    "        return R(clist)\n",
    "\n",
    "    def encrypt(self,h,m):\n",
    "        s=self.sample(self.n, self.Dm, self.Dm-1)\n",
    "        c=s*h+m\n",
    "        c=c%(x^self.n-1)\n",
    "        c=self.modCoeffs(c,self.q)\n",
    "        return c\n",
    "\n",
    "    def decrypt(self,c,Priv):\n",
    "        f,fp=Priv\n",
    "        a=f*c\n",
    "        a=a%(x^self.n-1)\n",
    "        a=self.modCoeffs(a,self.q)\n",
    "        a=a*fp\n",
    "        a=a%(x^self.n-1)\n",
    "        a=self.modCoeffs(a,self.p)\n",
    "        return a\n",
    "\n",
    "    def __inv_poly_mod2__(self,poly):\n",
    "        k=0;b=1;c=0*x;\n",
    "        f=poly;g=x^self.n-1\n",
    "        f=self.modCoeffs(f, 2)\n",
    "        res=False\n",
    "        while True:\n",
    "            while f(0)==0 and not f.is_zero():\n",
    "                f=f.shift(-1)\n",
    "                c=c.shift(1)\n",
    "                c=self.modCoeffs(c, 2)\n",
    "                k+=1\n",
    "            if f.is_one():\n",
    "                e=(-k)%self.n\n",
    "                retval= x^e*b \n",
    "                res=True\n",
    "                break\n",
    "            elif f.degree()==-1 or f.is_zero():\n",
    "                break\n",
    "            if f.degree()<g.degree():\n",
    "                f,g=g,f\n",
    "                b,c=c,b\n",
    "            f=f+g\n",
    "            b=b+c\n",
    "            f=self.modCoeffs(f, 2)\n",
    "            c=self.modCoeffs(c, 2)\n",
    "        if res:\n",
    "            retval=retval%(x^self.n-1)\n",
    "            retval=self.modCoeffs(retval, 2)\n",
    "            return True, retval\n",
    "        else:\n",
    "            return False,0\n",
    "\n",
    "    def __inv_poly_mod3__(self,poly):\n",
    "        k=0;b=1;c=0*x;\n",
    "        f=poly;g=x^self.n-1\n",
    "        res=False\n",
    "        while True:\n",
    "            while f(0)==0 and not f.is_zero():\n",
    "                f=f.shift(-1)\n",
    "                c=c.shift(1)\n",
    "                k+=1\n",
    "            if f.is_one():\n",
    "                e=(-k)%self.n\n",
    "                retval= x^e*b \n",
    "                res=True\n",
    "                break\n",
    "            elif (-f).is_one():\n",
    "                e=(-k)%self.n\n",
    "                retval= -x^e*b \n",
    "                res=True\n",
    "                break\n",
    "            elif f.degree()==-1 or f.is_zero():\n",
    "                break\n",
    "            if f.degree()<g.degree():\n",
    "                f,g=g,f\n",
    "                b,c=c,b\n",
    "            if f(0)==g(0):\n",
    "                f=f-g\n",
    "                b=b-c\n",
    "            else:\n",
    "                f=f+g\n",
    "                b=b+c \n",
    "            f=self.modCoeffs(f, 3)\n",
    "            c=self.modCoeffs(c, 3)\n",
    "        if res:\n",
    "            retval=retval%(x^self.n-1)\n",
    "            retval=self.modCoeffs(retval, 3)\n",
    "            return True, retval\n",
    "        else:\n",
    "            return False,0\n",
    "\n",
    "    def __inv_poly_mod_prime_pow__(self,poly):\n",
    "        res,b=self.__inv_poly_mod2__(poly)\n",
    "        if res:\n",
    "            qr=2\n",
    "            while qr<self.q:\n",
    "                qr=qr^2\n",
    "                b=b*(2-poly*b)\n",
    "                b=b%(x^self.n-1)\n",
    "                b=self.modCoeffs(b, self.q)\n",
    "            return True,b\n",
    "        else:\n",
    "            return False,0\n",
    "\n",
    "    def __gen_priv_key__(self):\n",
    "        res=False\n",
    "        while (res==False):\n",
    "            poly=self.__pn__(self.D1,self.D2,self.D3)\n",
    "            poly=1+2*poly\n",
    "            print (\"==poly==\")\n",
    "            ppInv=self.__inv_poly_mod3__(poly)[1]\n",
    "            print (\"==inv3==\")\n",
    "            res,pqInv=self.__inv_poly_mod_prime_pow__(poly)\n",
    "            print (\"==invq==\")\n",
    "        return poly,ppInv,pqInv\n",
    "\n",
    "    def gen_keys(self):\n",
    "        f,fp,fq=self.__gen_priv_key__()\n",
    "        g=self.sample(n,self.Dg,self.Dg-1)\n",
    "        h=self.p*g*fq\n",
    "        h=h%(x^self.n-1)\n",
    "        h=self.modCoeffs(h,self.q)\n",
    "        return h,(f,fp)\n",
    "\n",
    "    def __init__(self, SECLEVEL):\n",
    "        self.p=3\n",
    "        self.q=2048\n",
    "        if SECLEVEL==128:\n",
    "            self.n=439\n",
    "            self.D1=9;self.D2=8;self.D3=5\n",
    "            self.Dg=146;self.Dm=112\n",
    "        elif SECLEVEL==192:\n",
    "            self.n=593\n",
    "            self.D1=10;self.D2=10;self.D3=8\n",
    "            self.Dg=197;self.Dm=158\n",
    "        else:\n",
    "            self.n=743\n",
    "            self.D1=11;self.D2=11;self.D3=15\n",
    "            self.Dg=247;self.Dm=204\n",
    "\n",
    "ntru=NTRUEncrypt(256)\n",
    "n=ntru.n; dm=ntru.Dm\n",
    "m=ntru.sample(n,dm,dm-1)\n",
    "ts=time()\n",
    "h,Priv=ntru.gen_keys()\n",
    "kgtime= time()-ts\n",
    "ts=time()\n",
    "cc=ntru.encrypt(h,m)\n",
    "enctime=time()-ts\n",
    "ts=time()\n",
    "mm=ntru.decrypt(cc,Priv)\n",
    "dectime=time()-ts\n",
    "print kgtime,enctime,dectime\n",
    "print mm==m\n",
    "\n",
    "m1=ntru.sample(n,dm,dm-1)\n",
    "m2=ntru.sample(n,dm,dm-1)\n",
    "cc1=ntru.encrypt(h,m1)\n",
    "cc2=ntru.encrypt(h,m2)\n",
    "\n",
    "mm=ntru.decrypt(cc1+cc2,Priv)\n",
    "mmm=ntru.modCoeffs(m1+m2,ntru.p)\n",
    "print mm==mmm"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(True, x^3 + x^2 - 1)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "ntru.n = 5\n",
    "f = R([-1,1,0,0,1])\n",
    "ntru.__inv_poly_mod3__(f)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 8.9",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 2
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython2",
   "version": "2.7.15"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
